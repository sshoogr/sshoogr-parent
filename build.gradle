/*
 * Copyright (C) 2020 Aestas/IT
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

def resolveProperty(Project project, String envKey, String propKey, String defaultValue) {
    String value = System.getenv(envKey)
    if (!value) value = project.findProperty(propKey)
    !value? defaultValue : value
}

config {
    release = resolveProperty(project, 'SSHOOGR_RELEASE', 'release', 'false').toBoolean()

    info {
        name          = 'Sshoogr parent'
        description   = 'Parent for sshoogr projects'
        vendor        = 'AestasIT'
        inceptionYear = '2020'
        tags          = ['sshoogr', 'pom', 'build']

        links {
            website      = "https://github.com/sshoogr/${project.rootProject.name}"
            issueTracker = "https://github.com/sshoogr/${project.rootProject.name}/issues"
            scm          = "https://github.com/sshoogr/${project.rootProject.name}.git"
        }

        scm {
            url                 = "https://github.com/sshoogr/${project.rootProject.name}"
            connection          = "scm:git:https://github.com/sshoogr/${project.rootProject.name}.git"
            developerConnection = "scm:git:git@github.com:sshoogr/${project.rootProject.name}.git"
        }

        people {
            person {
                id    = 'aadamovich'
                name  = 'Andrey Adamovich'
                roles = ['developer', 'author']
            }
            person {
                id    = 'aalmiray'
                name  = 'Andres Almiray'
                url   = 'http://andresalmiray.com/'
                roles = ['developer']
                properties = [
                    twitter: 'aalmiray',
                    github : 'aalmiray'
                ]
            }
        }

        specification  { enabled = false }

        implementation { enabled = false }

        repositories {
            repository {
                name = 'localRelease'
                url  = "${project.rootProject.buildDir}/repos/local/release"
            }
            repository {
                name = 'localSnapshot'
                url  = "${project.rootProject.buildDir}/repos/local/snapshot"
            }
        }
    }

    docs {
        javadoc {
            enabled = false
        }

        groovydoc {
            replaceJavadoc = true
            options {
                link 'https://docs.gradle.org/5.0/javadoc/', 'org.gradle.'
            }
        }
    }

    licensing {
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }

    bintray {
        enabled = false
    }

    publishing {
        signing             = false
        releasesRepository  = 'localRelease'
        snapshotsRepository = 'localSnapshot'
    }
}

allprojects {
    repositories {
        jcenter()
        gradlePluginPortal()
        mavenLocal()
    }
}

projects.subprojects {
    dir('plugins') {
        config {
            info {
                description = project_description
            }

            plugins {
                plugin {
                    name                = project.name
                    id                  = project.pluginId
                    implementationClass = project.pluginImplementationClass
                }
            }
        }

        dependencies {
            compileOnly gradleApi()
        }

        normalization {
            runtimeClasspath {
                ignore('/META-INF/MANIFEST.MF')
            }
        }
    }
}